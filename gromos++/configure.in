dnl configure.in for GROMOS++
dnl Written by Roland Buergi, modified for gsl by M. Kastenholz
dnl and again by Markus Christen
dnl 12/20/00

AC_INIT(src/gromos/Exception.h)
AC_CONFIG_AUX_DIR(config)
AC_CANONICAL_HOST

AC_LANG(C++)

GROMOS_MAJOR_VERSION=0
GROMOS_MINOR_VERSION=1
GROMOS_MICRO_VERSION=1

GROMOS_VERSION=$GROMOS_MAJOR_VERSION.$GROMOS_MINOR_VERSION.$GROMOS_MICRO_VERSION
AC_DEFINE_UNQUOTED(GROMOS_VERSION, "$GROMOS_VERSION", [Gromos veersion info])

CONF_DATE=`date`
AC_DEFINE_UNQUOTED(GROMOS_DATE, "$CONF_DATE", [MD date (configure)])

echo ""
echo "Gromos++"
echo "analysis package of Gromos03"
echo "version ${GROMOS_VERSION}"
echo ""
echo ""

AM_INIT_AUTOMAKE(gromos, $GROMOS_VERSION, no-define)
AM_CONFIG_HEADER(config.h)

dnl Initialise libtool
AM_PROG_LIBTOOL

dnl Check if all programmes are there
AC_PROG_CXX
AC_PROG_MAKE_SET

dnl Checks for header files.
AC_HEADER_STDC
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T

AC_CHECK_FUNCS(hypot)
AC_CHECK_FUNCS(expm1)
AC_CHECK_FUNCS(acosh)
AC_CHECK_FUNCS(asinh)
AC_CHECK_FUNCS(atanh)

AC_CHECK_FUNCS(isinf, ,[
    AC_MSG_CHECKING([for isinf with <math.h>])
    AC_TRY_LINK([#include <math.h>], [float f = 0.0; isinf(f)],[
        AC_MSG_RESULT(yes)
        AC_DEFINE(HAVE_ISINF)],
        AC_MSG_RESULT(no))])

AC_CHECK_FUNCS(finite, ,[
    AC_MSG_CHECKING([for finite with <math.h>])
    AC_TRY_LINK([#include <math.h>], [float f = 0.0; finite(f)],[
        AC_MSG_RESULT(yes)
        AC_DEFINE(HAVE_FINITE)
        ac_cv_func_finite=yes],
        AC_MSG_RESULT(no))])

if test "x${ac_cv_func_finite}" != "xyes" ; then
  AC_CHECK_FUNCS(isfinite, ,[
      AC_MSG_CHECKING([for isfinite with <math.h>])
      AC_TRY_LINK([#include <math.h>], [float f = 0.0; isfinite(f)],[
          AC_MSG_RESULT(yes)
          AC_DEFINE(HAVE_ISFINITE)],
          AC_MSG_RESULT(no))])
fi

AC_CHECK_FUNCS(isnan, ,[
    AC_MSG_CHECKING([for isnan with <math.h>])
    AC_TRY_LINK([#include <math.h>], [float f = 0.0; isnan(f)],[
        AC_MSG_RESULT(yes)
        AC_DEFINE(HAVE_ISNAN)],
        AC_MSG_RESULT(no))])

dnl OpenBSD has a broken implementation of log1p.
case "$host" in
    *-*-*openbsd*)
       AC_MSG_RESULT([avoiding OpenBSD system log1p - using gsl version])
       ;;
    *)
       AC_CHECK_FUNCS(log1p)
       ;;
esac

dnl
dnl FIXME: on Solaris we will need a prototype for hypot, since it is
dnl missing from their math.h according to Jerry.
dnl
dnl extern double hypot(double, double);
dnl


dnl set some defaults to CXXFLAGS and LDFLAGS
if eval "test x$GCC = xyes"; then
  if echo "$CXXFLAGS" | grep "\-Wall" > /dev/null 2> /dev/null; then
    CXXFLAGS="$XXCFLAGS"
  else
    CXXFLAGS="$CXXFLAGS -Wall"
  fi
fi

AC_PREFIX_DEFAULT(`pwd`)

dnl Checks for libraries.
dnl Replace `main' with a function in -lm:
AC_CHECK_LIB(m, sqrt)

dnl check for gsl
dnl the gnu scientific library
dnl www.gnu.org/software/gsl
AC_MSG_CHECKING(gnu scientific library (libgsl))
AC_CHECK_HEADER(gsl/gsl_rng.h, , [
AC_MSG_ERROR([header files for libgsl not found make sure the gsl headers
		     are in the standard include directories or add the path to CPPFLAGS. 
		     if you do not have gsl installed, install it first. 
		     gsl-1.3.tar.gz is provided together with gromoms++ ])  
])

AC_SEARCH_LIBS(cblas_daxpy, gslcblas, ,[
AC_MSG_ERROR([libgsl not found. make sure libgsl is installed and if necessary
		     add it to CXXFLAGS and LDFLAGS if you do not have gsl installed,
		     install it first. gsl-1.3.tar.gz is provided together with gromos++ ])])

AC_SEARCH_LIBS(gsl_rng_alloc, gsl, , [AC_MSG_ERROR([libgsl not found. make sure libgsl is installed
		     and if necessary add it to CXXFLAGS, CFLAGS and LDFLAGS
		     if you do not have gsl installed, install it first. gsl-1.3.tar.gz is provided
		     together with gromos++])])

AC_MSG_RESULT(yes)


dnl check if full STL support
AC_CV_CXX_VERSION_OK
if test "$ac_cv_cxx_version_ok" = no; then
  echo "Your C++ compiler does not support the STL"
  echo "to the right extent. Check your C++ comipler..."
  exit
fi

AC_CACHE_SAVE

AC_SUBST(GROMOS_MAJOR_VERSION)
AC_SUBST(GROMOS_MINOR_VERSION)
AC_SUBST(GROMOS_MICRO_VERSION)
AC_SUBST(GROMOS_VERSION)

AC_OUTPUT(
src/Makefile
src/gmath/Makefile
src/gromos/Makefile
src/gcore/Makefile
src/gio/Makefile
src/bound/Makefile
src/fit/Makefile
src/args/Makefile
src/utils/Makefile
programs/Makefile
Makefile)

echo ""
echo "configure finished successfully"
echo "type make"
echo ""

